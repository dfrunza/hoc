proc int
max(int a, int b)
{
  var int result = a;
  if(a < b)
    result = b;
  return result;
}

proc int
min(int a, int b)
{
  var int result = a;
  if(a > b)
    result = b;
  return result;
}

proc float
fmax(float a, float b)
{
  var float result = a;
  if(a < b)
    result = b;
  return result;
}

proc float
fmin(float a, float b)
{
  var float result = a;
  if(a > b)
    result = b;
  return result;
}

proc int
abs(int a)
{
  if(a < 0)
    a = -a;
  return a;
}

proc float
fabs(float a)
{
  if(a < 0.0)
    a = -a;
  return a;
}

proc float
ipow(float a, int p)
{
  var bool inv = (p < 0);
  p = abs(p);

  var float result = 1.0;
  if(p != 0)
  {
    while(p > 0)
    {
      result = result*a;
      p = p - 1;
    }
    if(inv)
      result = 1.0/result;
  }
  return result;
}


proc float
sqrt_iter(float x, float g)
{
  var float r = x/g;

  if(r != g)
  {
    r = (r+g)/2.0;
    if(fabs(r-g) == 1.0)
      r = fmin(r, g);
  }
  return r;
}

/*
   Computes the largest number 'r' such that 'r*r <= x'
 */
proc float
sqrt(float x)
{
  var float g = x;
  var float r = 1.0;

  while((g > 0.0) && (g != r))
  {
    g = r;
    r = sqrt_iter(x, g);
  }
  return g;
}

/*
   Integer log_b of n : the largest integer x such that
      pow(b, x) <= n
*/
proc int
floor_log_b(int b, float n)
{
  var int x = 0;
  var float b_pow_x = 1;

  if(n > 1.0)
  {
    while(n > b_pow_x)
    {
      x = x + 1;
      b_pow_x = ipow(b, x);
    }
    x = x - 1;
  }
  else if(n < 1.0)
  {
    while(b_pow_x > n)
    {
      b_pow_x = ipow(b, x);
      x = x - 1;
    }
    x = x + 1;
  }

  return x;
}

/* Returns the number of significand 10-base digits */
proc int
fp_bin_to_dec(float v, int* significand, int* exponent)
{
  var float v_abs = fabs(v);
  var int x = floor_log_b(10, v_abs) + 1;
  *exponent = x;

  var float B_power_x = ipow(10.0, -x);
  var float v_prime = v_abs * B_power_x;

  var int k = 0;
  var float R = v_prime;
  var float M = ipow(2.0, -23) / 2.0; /* -23 is the width of the significand of single precision binary FP */
  var int U;

  var bool run_loop = true;
  while(run_loop)
  {
    k = k + 1;
    U = cast(int)(R * 10.0);
    R = (R * 10.0) - U;
    M = M * 10.0;
    significand[k] = U;

    run_loop = (R >= M) && (R <= (1.0 - M));
  }

  if(R <= 0.5)
    significand[k] = U;
  else
    significand[k] = U + 1;

  return k;
}
